//----------------------------------------------------------------------------
// Copyright (C) 2003  Rafael H. Bordini, Jomi F. Hubner, et al.
// 
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 2.1 of the License, or (at your option) any later version.
// 
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
// 
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
// 
// To contact the authors:
// http://www.dur.ac.uk/r.bordini
// http://www.inf.furb.br/~jomi
//
// CVS information:
//   $Date$
//   $Revision$
//   $Log$
//   Revision 1.3  2005/12/18 15:31:02  jomifred
//   no message
//
//   Revision 1.2  2005/12/17 17:36:20  jomifred
//   no message
//
//   Revision 1.1  2005/12/08 20:13:53  jomifred
//   changes for JasonIDE plugin
//
//   Revision 1.18  2005/10/29 21:46:22  jomifred
//   add a new class (MAS2JProject) to store information parsed by the mas2j parser. This new class also create the project scripts
//
//   Revision 1.17  2005/09/20 17:01:22  jomifred
//   no message
//
//   Revision 1.16  2005/09/02 19:19:16  jomifred
//   add files in project lib dir on classpath
//
//   Revision 1.15  2005/08/23 17:11:13  jomifred
//   fix bug in the script that starts saci
//
//   Revision 1.14  2005/08/15 13:10:41  jomifred
//   using infrastructure instead of architecture in mas2j
//
//   Revision 1.13  2005/08/12 21:12:16  jomifred
//   add cvs keywords
//
//----------------------------------------------------------------------------


options {
  STATIC=false;
}

PARSER_BEGIN(mas2j)

package jason.mas2j.parser;

import java.util.*;
import java.io.*;
import jason.mas2j.*;
import jIDE.*;

public class mas2j {


    MAS2JProject project;

    // Run the parser
    public static void main (String args[]) {

      String name;
      mas2j parser;
      MAS2JProject project = new MAS2JProject();
      
      if (args.length==1) {
        name = args[0];
        System.err.println("mas2j: reading from file " + name + " ..." );
		try {
		  parser = new mas2j(new java.io.FileInputStream(name));
		} catch(java.io.FileNotFoundException e){
		  System.err.println("mas2j: file \"" + name + "\" not found.");
		  return;
        }
      } else {
		System.out.println("mas2j: usage must be:");
		System.out.println("      java mas2j <MASConfFile>");
		System.out.println("Output to file <MASName>.xml");
        return;
      }

      // parsing
      try {
		project = parser.mas();
		Config.get().fix();
        project.setDirectory(new File(".").getAbsolutePath());
		System.out.println("mas2j: "+name+" parsed successfully!\n");
		project.writeXMLScript();
        project.writeScripts();

        int step = 1;
        System.out.println("To run your MAS:");
        //System.out.println("  1. chmod u+x *.sh");
        System.out.println("  "+step+". compile the java files (script ./compile-"+project.getSocName()+".sh)");
        step++;
        if (project.getInfrastructure().equals("Saci")) {
             System.out.println("  "+step+". run saci (script ./saci-"+project.getSocName()+".sh)");
             step++;
        }
        System.out.println("  "+step+". run your agents (script ./"+project.getSocName()+".sh)");
      }
      catch(ParseException e){
		System.err.println("mas2j: parsing errors found... \n" + e);
      }
    }

}

PARSER_END(mas2j)

SKIP : {
 " "
|  "\t"
|  "\n"
|  "\r"
|  <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
|  <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

TOKEN : {

// Predefined
   <MAS:         "MAS">
|  <AGS:         "agents">
|  <ENV:         "environment">
|  <CONTROL:     "executionControl">
|  <AT:          "at">
|  <ARCH:        "architecture" >
|  <INFRA:       "infrastructure" >
|  <INFRAV:       ("Centralised" | "Saci") >

|  <ASOEE:       "events" >
|  <ASOEEV:      ("discard"|"requeue"|"retrieve") >

|  <ASOIB:       "intBels">
|  <ASOIBV:      ("sameFocus"|"newFocus") >

|  <ASONRC:      "nrcbp" >

|  <ASOV:        "verbose" >

|  <ASOSYNC:     "synchronised" >
|  <ASOBOOL:     ("true" | "false" ) >

|  <ASAGCLASS:      "agentClass" >
|  <ASAGARCHCLASS:  "agentArchClass" >

// Numbers
|  <NUMBER: ["0"-"9"] (["0"-"9"])* >

// Strings
|  <STRING: "\"" ( ~["\"","\\","\n","\r"]
                 | "\\" ( ["n","t","b","r","f","\\","\'","\""]
                        | ["0"-"7"] (["0"-"7"])?
                        | ["0"-"3"] ["0"-"7"] ["0"-"7"]))* "\"">

// Identifiers
|  <ASID: <LC_LETTER> (<LETTER>|<DIGIT>|"_")* >
|  <CLASSID: <LETTER> (<LETTER>|<DIGIT>|"_")* >

// Path
|  <PATH: ( "./" | "/" | "\\" | (<DRIVER>) ) ( (<LETTER>|<DIGIT>|"_")* ( "/" | "\\" ) )* >
|  <DRIVER: (<LETTER> ":")>

|  <LETTER: ( <LC_LETTER> | <UP_LETTER> )>
|  <LC_LETTER: ["a"-"z"]>
|  <UP_LETTER: ["A"-"Z"]>
|  <DIGIT: ["0"-"9"]>
}



/* Configuration Grammar */

MAS2JProject mas() :           { Token soc; }
{
  <MAS>
  soc = <ASID>                 { project = new MAS2JProject();
                                 project.setSocName(soc.image); }
  "{" 
  
  infra()

  environment()

  control()
  
  agents()

  "}"
                            { return project; }
}

void infra() :              { Token t; }
{                           { project.setInfrastructure("Centralised"); }
  [ (<ARCH>                 { System.err.println("The id <architecture> was replaced by <infrastructure> in .mas2j syntax, please use the new id."); }
     | <INFRA>) 
    ":" t = <INFRAV>        { project.setInfrastructure(t.image); } 
  ]
}


void agents() :             { project.initAgMaps(); }
{
  <AGS> ":"
  ( agent() )+
}


void agent() :              { Token agName; 
                              Token qty; Token value; 
                              Token host; 
                              AgentParameters ag = new AgentParameters();
                            }
{
  agName = <ASID>           

                            { ag.name = agName.image;
                              ag.asSource = new File(agName.image+".asl"); 
                            }
                            
  [ ag.asSource  = fileName() ]

  ag.options = ASoptions()        


  [ <ASAGARCHCLASS> ag.archClass = className() ]

  [ <ASAGCLASS> ag.agClass = className()  ]

  [ "#" qty = <NUMBER>      { ag.qty = Integer.parseInt(qty.image); } ] 

  [ <AT> host = <STRING>    { ag.host = host.image; } ] 

  ";"
                            { project.addAgent(ag); } 
}


File fileName() :           { String path = ""; 
                              Token t; 
                              Token i;
                              Token e; 
                              String ext = ".asl"; }
{
  [ t = <PATH>              { path = t.image; } ]
    i = <ASID>               
  [ "." e = <ASID>          { ext = "." + e.image; } ]
                            { //if (!path.startsWith(File.separator)) {
                              //  path = destDir + path;
                              //}
                              return new File( path + i.image + ext);
                            }
}


String className() :       {Token c; String p = "";}
{
   (c = <CLASSID> | c = <ASID> )
   [ "." p = className()   { return c.image + "." + p; } ]
                           { return c.image; }
}

Map ASoptions() :          { Map opts = new HashMap(); }
{
  [ "[" opts = procOption(opts) ( "," opts = procOption(opts) )* "]" ]
  {
    return opts; 
  }
}

Map procOption(Map opts): { Token opt; Token oval; }
{
  ( opt=<ASOEE>   "=" oval=<ASOEEV>  {opts.put(opt.image,oval.image);}
  | opt=<ASOIB>   "=" oval=<ASOIBV>  {opts.put(opt.image,oval.image);}
  | opt=<ASOSYNC> "=" oval=<ASOBOOL> {opts.put(opt.image,oval.image);}
  | opt=<ASONRC>  "=" oval=<NUMBER>  {opts.put(opt.image,oval.image);} 
  | opt=<ASOV>    "=" oval=<NUMBER>  {opts.put(opt.image,oval.image);} 
  | opt=<ASID>    "=" 
    (oval=<STRING>|oval=<ASID>|oval=<NUMBER>)
                                     {opts.put(opt.image,oval.image);} 
  )
                                     { 
                                       return opts; 
                                     }
}


void environment() :        { Token host = null; String envClass = null;}
{

  [  <ENV> ":"
     envClass = className() 
     [ <AT> host = <STRING> ]
  ]   
                            { 
                              project.setEnvClass(envClass);
                              if (host != null) {
		                          project.setEnvHost(host.image);
	                          }
                            }
}


void control() :            { Token host =  null; }
{
                            { String tControlClass = null; 
                            }
                            
  [ <CONTROL> ":" tControlClass = className() 
    [ <AT> host = <STRING> ]
  ]
                            { project.setControlClass(tControlClass);
                              if (host != null) {
                                   project.setControlHost(host.image);
                              }
                            }
}
