// Copyright (C) 2003  Rafael H. Bordini, Jomi F. Hubner, et al.
// 
// This library is free software; you can redistribute it and/or
// modify it under the terms of the GNU Lesser General Public
// License as published by the Free Software Foundation; either
// version 2.1 of the License, or (at your option) any later version.
// 
// This library is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
// Lesser General Public License for more details.
// 
// You should have received a copy of the GNU Lesser General Public
// License along with this library; if not, write to the Free Software
// Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
// 
// To contact the authors:
// http://www.dur.ac.uk/r.bordini
// http://www.inf.furb.br/~jomi
//
// CVS information:
//   $Date$
//   $Revision$
//   $Log$
//   Revision 1.27  2006/02/22 21:19:05  jomifred
//   The internalAction removePlan use plan's label as argument instead of plan's strings
//
//   Revision 1.26  2006/01/02 13:49:01  jomifred
//   add plan unique id, fix some bugs
//
//   Revision 1.25  2005/12/31 16:29:58  jomifred
//   add operator =..
//
//   Revision 1.24  2005/12/30 20:40:17  jomifred
//   new features: unnamed var, var with annots, TE as var
//
//   Revision 1.23  2005/12/23 00:48:23  jomifred
//   StringTerm is now an interface implemented by StringTermImpl
//
//   Revision 1.22  2005/12/22 00:04:18  jomifred
//   ListTerm is now an interface implemented by ListTermImpl
//
//   Revision 1.20  2005/08/18 01:22:21  jomifred
//   (AS grammar) arithmeticExpression renamed to ae
//
//   Revision 1.19  2005/08/17 18:19:21  jomifred
//   change AS grammar (unify lt and la implementation)
//
//----------------------------------------------------------------------------


options {
   FORCE_LA_CHECK=true;
   STATIC=false;
}

PARSER_BEGIN(as2j)

  package jason.asSyntax.parser;

  import java.util.*;
  import java.io.*;
  import jason.asSyntax.*;
  import jason.asSemantics.*;
  import jason.*;

  public class as2j {

    private String name;
    private File source = null;

    private PrintStream out = System.out;

    /* Auxiliary Functions */

    void setName(String n) {
    	name = n;
    }

    void setSource(File f) {
        source = f;
    }
  }

PARSER_END(as2j)

SKIP : {
 " "
|  "\t"
|  "\n"
|  "\r"
|  <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
|  <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

TOKEN : {
// Predefined
  <TK_TRUE: "true">
| <TK_FALSE: "false">
| <TK_NOT:  "not">
| <TK_NEG:  "~">
| <TK_INTDIV: "div">

// special chars
| <TK_LABEL_AT: "@">

// Numbers
|   <NUMBER: ["0"-"9"] (["0"-"9"])*
          | (["0"-"9"])* "." (["0"-"9"])+ (<EXPONENT>)?
          | (["0"-"9"])+ <EXPONENT> >
|  <EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+>

// Strings
|  <STRING: "\"" ( ~["\"","\\","\n","\r"]
                 | "\\" ( ["n","t","b","r","f","\\","\'","\""]
                        | ["0"-"7"] (["0"-"7"])?
                        | ["0"-"3"] ["0"-"7"] ["0"-"7"]))* "\"">

// Identifiers
|  <ATOM      : (<LC_LETTER> | "." <CHAR>) (<CHAR> | "." <CHAR>)*>
|  <VAR       :  <UP_LETTER> (<CHAR>)*>
|  <UNNAMEDVAR:  "_">
|  <CHAR      : (<LETTER> | <DIGIT> | "_")>
|  <LETTER    : ( <LC_LETTER> | <UP_LETTER> )>
|  <LC_LETTER : ["a"-"z"]>
|  <UP_LETTER : ["A"-"Z"]>
|  <DIGIT     : ["0"-"9"]>
}

/* AgentSpeak Grammar */

void ag(Agent a) : { List bbl = new LinkedList(); PlanLibrary pp;  }
{
  bs(bbl)
  pp = ps()
  <EOF>
              { if (a != null) {
                  try {
                    a.getPS().addAll(pp); 
  
                    // add beliefs (when the plans are loaded, so the events are relevant
                    Iterator i = bbl.iterator();
                    while (i.hasNext()) {
                       Literal l = (Literal)i.next();
                       Circumstance c = null;
                       try {
                          c = a.getTS().getC();
                       } catch (Exception e) {}
                       a.addBel(l, BeliefBase.TSelf, c, Intention.EmptyInt);
                    }
                  } catch (JasonException je) {
                     System.err.println("Error:"+je);
                  }
                }
              }
}

/* Beliefs */
void bs(List bbl) : { Literal F; }
{
  ( F=l() "."       {  if(!F.isGround()) {
				            throw new ParseException("Error: Initial belief must be ground!\n       Cannot use "+F+" as initial belief.");
                       }
                       bbl.add(F);
                    } 
  )*
}

/* Plans */
PlanLibrary ps() : { PlanLibrary pp = new PlanLibrary(); Plan pl;  }
{
  ( pl = p()       { try {
                       pp.add(pl); 
                     } catch (JasonException je) {
                       System.err.println("Error adding plan:"+je);
                     }
                   } 
  )*
                   { return pp; }
}

/* Plan */
Plan p() :         { Token k; Trigger T; ArrayList C; ArrayList H; Pred L = null; int start = -1, end;}
{
    [ k = <TK_LABEL_AT> L=at()  
                   { start = k.beginLine; } 
    ]
    T=te()         
    k = ":"        { if (start == -1) start = k.beginLine; }
    C=ct() 
    "<-" H=bd() 
    k = "." 
                   { end = k.beginLine;
                     Plan p = new Plan(L,T,C,H);  
                     p.setSourceLines(start,end);
                     return p;}
}

/* Trigger */
Trigger te() : { 
               		boolean teType;
			    	byte    teGoal = Trigger.TEBel;
				 	Literal F; 
               }
{
  ( "+"        { teType = Trigger.TEAdd; }
  | "-"        { teType = Trigger.TEDel; }
  )
  
  [ ( "!"      { teGoal = Trigger.TEAchvG; }
    | "?"      { teGoal = Trigger.TETestG; }
    )
  ]
  
  ( F=l()
  | F=var()
  )            { return new Trigger(teType,teGoal,F); }
}

/* Literal */
Literal l() :  { Pred F; Token k; boolean type = Literal.LPos; }
{
  ( ( [ <TK_NEG>              { type = Literal.LNeg; }
      ]
      F=at()                  { return new Literal(type,F); }
    )
  | k=<TK_TRUE>               { return Literal.LTrue;}  // used when dl/body is true
  | k=<TK_FALSE>              { return Literal.LFalse; } 
  )
}


/* Plan context */
ArrayList ct() :    { DefaultLiteral P; ArrayList C = new ArrayList(); List CT;}
{
     P = dl()       { if (!P.getLiteral().equals(Literal.LTrue)) {
                        C.add(P); 
                      }
                    }
     [ "&" CT=ct()  { C.addAll(CT); } 
     ]
                    { return C; }
}


DefaultLiteral dl() : { boolean isPos = DefaultLiteral.LDefPos; 
                        Term rel = null;
                      }
{
  [ <TK_NOT>          { isPos = DefaultLiteral.LDefNeg; }
  ]

  rel = re() 
                      { if (rel.isVar() || rel.isLiteral()) {
                          return new DefaultLiteral(isPos, (Literal)rel);
                        } else {
                          return new DefaultLiteral(isPos, new Literal(rel)); 
                        }
                      }
                     
}


/* Plan body */
ArrayList bd() : { BodyLiteral F; 
                   ArrayList BD = new ArrayList(); 
                   List BD2;}
{
  F=f()          { if (! F.getLiteral().equals(Literal.LTrue)) {
                      BD.add(F); 
                   } 
                 }
                 
  [ ";" BD2=bd() { BD.addAll(BD2); } ]
                 { return BD; }
}


BodyLiteral f() :     { Literal A; Token k;
                        byte formType = BodyLiteral.HAction;
                        Term rel;
                        VarTerm v;
                      }
{
 
 ( LOOKAHEAD(4)
   // goals and belief updates
   (  "!"             { formType = BodyLiteral.HAchieve; }
    | "!!"            { formType = BodyLiteral.HAchieveNF; }
    | "?"             { formType = BodyLiteral.HTest; }
    | "+"             { formType = BodyLiteral.HAddBel; }
    | "-"             { formType = BodyLiteral.HDelBel; }
   )
   ( A=l()            { return new BodyLiteral(formType,A); }
   | v = var()        { return new BodyLiteral(formType,v); } // used in communication "+C"
   )
 |
  // actions or expressions
  rel = re() 
                      { if (rel.isVar() || rel.isLiteral()) {
                          return new BodyLiteral(formType, (Literal)rel);
                        } else {
                          return new BodyLiteral(formType, new Literal(rel)); 
                        }
                      }
 )
}


/* Annotated Atomic Formulae */
Pred at() :         { Token K; Pred p = new Pred(); Term t; List l; ListTerm lt;}
{
  K=<ATOM>          { p.setFunctor(K.image); } 
  [
      "(" l = lt() 
      ")"           { p.addTerms(l); }
  ]
  [ 
      lt = list()   { p.setAnnots(lt); }
  ] 
  { return p; }
}


/* List of terms */
List lt() :         { List listTerms = new ArrayList(); Term v; }
{
  v=t()             { listTerms.add(v); } 
  ( "," v=t()       { listTerms.add(v); }
  )* 
                    { return listTerms; }
}


Term t() :         { Token K; Term u; NumberTerm nb;}
{
  ( u=l()          { return u; }
  | u=list()       { return u; } 
  | nb=ae()        { return (Term)nb; }
  | K=<STRING>     { return new StringTermImpl(K.image.replaceAll("\"", "\\\"")); } 
  )
}


ListTermImpl list() : { ListTermImpl lt = new ListTermImpl(); ListTerm last; 
                        Token K; Term f; }
{
  "["
  [
  f=t()                   { last = lt.add(f); }
  ( "," f=t()             { last = last.add(f); }
  )*
  [ "|"  ( K=<VAR>        { last.setNext(new VarTerm(K.image)); }
         | K=<UNNAMEDVAR> { last.setNext(new UnnamedVar()); }
         | f=list()       { last = last.concat((ListTerm)f);	   }
         )           
  ]
  ]
  "]"                     { return lt; } 
}


/* relational expression 
   used in context, body and term
   
     <VAR>      [ <OPREL> <EXP> ]  --> this method returns the VarTerm
   | <LITERAL>  [ <OPREL> <EXP> ]  --> returns the Literal
   | <EXP> <OPREL> <EXP>           --> returns a Term like .gt(<Term>,<Term>)
*/
Term re() :                               {  Token k;
                                             Term op1 = null;
                                             Term op2 = null;
                                             NumberTerm nb; 
                                             String operator = "";
                                             boolean change = false;
                                             Term rel = null;
                                           }
{
    (   nb = ae()                          { op1 = (Term)nb; }
      | op1 = l()
      | k = <STRING>                       { op1 = new StringTermImpl(k.image); }
    )
                     
    [
       ( "<"                               { operator = ".gt"; change = true; } 
       | "<="                              { operator = ".gte"; change = true; } 
       | ">"                               { operator = ".gt"; } 
       | ">="                              { operator = ".gte"; } 
       | "=="                              { operator = ".equals"; } 
       | "\\=="                            { operator = ".notequals"; } 
       | "="                               { operator = ".unifies"; } 
       | "=.."                             { operator = ".literalBuilder"; } 
       )

       (   nb = ae()                       { op2 = (Term)nb; }
         | op2 = l()
         | k = <STRING>                    { op2 = new StringTermImpl(k.image); }
         | op2 = list()
       )

                                           { rel = new Term(operator);
                                             if (!change) {
                                                 rel.addTerm(op1); 
                                                 rel.addTerm(op2); 
                                 			  } else {
                                                 rel.addTerm(op2); 
                                                 rel.addTerm(op1); 
                                             }
                                           }
    ]
                                           { if (rel == null && !op1.isVar() && !op1.isLiteral()) {
                                                throw new ParseException("RelExp: second operand expected");
                                             }
                                             if (rel != null) {
                                                return rel; 
                                             } else { // op1 is a Var or Literal
                                                return op1;
                                             }
                                           }
}


/* arithmetic expression */
NumberTerm ae() :    { NumberTerm t1, t2; int op;}
{
   t1 = trm()        { op = -1; }
[  ( "+"             { op = ExprTerm.EOplus;} 
   | "-"             { op = ExprTerm.EOminus;}
   )
   t2 = ae()         { return new ExprTerm(t1,op,t2); }
]                    { return t1; }
}

NumberTerm trm() :  { NumberTerm t1, t2; int op;}
{
   t1 = factor()    { op = -1; }
[  ( "*"            { op = ExprTerm.EOtimes; } 
   | "/"            { op = ExprTerm.EOdiv; }
   | <TK_INTDIV>    { op = ExprTerm.EOintdiv; }
   )
   t2 = trm()       { return new ExprTerm(t1,op,t2); }
]                   { return t1; }
}

NumberTerm factor():{ NumberTerm t1, t2; int op;}
{
   t1 = simple()    { op = -1; }
[  ( "%"            { op = ExprTerm.EOmod; } 
   | "**"           { op = ExprTerm.EOpow; }
   )
   t2 = factor()    { return new ExprTerm(t1,op,t2); }
]                   { return t1; }
}


NumberTerm simple():{ Token K; NumberTerm t; VarTerm v; }
{
 ( K = <NUMBER>     { return new NumberTermImpl(K.image); }
 //| K = <VAR>        { return new VarTerm(K.image); }
 //| K = <UNNAMEDVAR> { return new UnnamedVar(); }
 | "-" t = simple() { return new ExprTerm(ExprTerm.EOminus, t); }
 | "(" t = ae() ")" { return t; }
 | v = var()        { return v; }
 )
}

VarTerm var():      { Token K; VarTerm v; ListTerm lt; }
{
 ( K = <VAR>        { v = new VarTerm(K.image); }
 | K = <UNNAMEDVAR> { v = new UnnamedVar(); }
 )
 [ 
   lt = list()      { v.setAnnots(lt); }
 ]
                    { return v; }
}

